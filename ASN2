//KC03K8EA JOB ,'Anil Singh',MSGCLASS=H
//STEP1    EXEC  PGM=ASSIST
//STEPLIB    DD  ****INFORMATION WITHELD*****************
//SYSPRINT   DD  SYSOUT=*
//SYSIN      DD  *
**********************************************************
*
*  Program:     ASSIGN2
*  Programmer:  Anil Singh (Z1666991)
*
*  Register usage:
*           R2,R3: TOTAL, COUNTER
*           R4,R5,R6: COLUMN1,COLUMN2,COLUMN3 (ON A CARD)
*           R7 : COLUMN1+COLUMN2+COLUMN3
*
*  Acknoledgements:
*      Developed using main-env-simulator by Tony Zhang
*      http://code.google.com/p/mainframe-env-simulator/
***********************************************************
ASSIGN2   CSECT
          USING ASSIGN2,15          R15 IS BASE REGISTER
          XREAD CARD,80             READ FIRST CARD
          SR    2,2                 INITIALIZE R2 FOR TOTAL
          SR    3,3                 INITIALIZE R3 FOR COUNTER
          XPRNT EMPTY,132           PRINT A BLANK LINE FOR COSMETICS
          XPRNT HEADPADL,120        PRINT A NICE HEAD TITLE
LOOP      BC    B'0100',EXIT        IF END OF FILE (EOF) THEN EXIT
          XDECI 4,CARD              GET 1ST NUMBER FROM CARD
          BC    B'0001',NXT         IF INVALID ENTRY: SKIP CARD
          XDECI 5,0(1)              GET 2ND NUMBER FROM CARD
          BC    B'0001',NXT         IF INVALID ENTRY: SKIP CARD
          XDECI 6,1(1)              GET 3RD NUMBER FROM CARD
          BC    B'0001',NXT         IF INVALID ENTRY: SKIP CARD
          SR    7,7                 INITIALIZE R7 FOR SUM
          AR    7,4                 R7=R7+R4
          AR    7,5                 R7=R7+R5
          AR    7,6                 R7=R7+R6
          AR    2,7                 R2=R2+R7(TOTAL+=SUM)
          A     3,INC               R3=R3+I (COUNTER++)
          XDECO 4,COL1              R4--DECIMAL->COL1
          XDECO 5,COL2              R5--DECIMAL->COL2
          XDECO 6,COL3              R6--DECIMAL->COL3
          XDECO 7,SUMOUT            R7--DECIMAL->SUMOUT
          XPRNT CRG,97              PRINT STUFF:THIS CARD
NXT       XREAD CARD,80             READ THE NEXT CARD
          BC    B'1011',LOOP        GO TO THE TOP OF LOOP
EXIT      XDECO 2,TOTAL             R2--DECIMAL->TOTAL    
          XDECO 3,NLINES            R3--DECIMAL->NLINES
          XPRNT FOOT,70             PRINT RESULTS ON FOOTER
          XPRNT MYEND,120           PRINT AND ENDLINE STRING
          BCR   B'1111',14          EXIT FROM PROGRAM
**************************************************************
* FOLLOWING DOES NOT GENERATE ANY MACHINE STATEMENTS
* ************************************************************
CARD      DS    CL80                CARD INPUT AREA
INC       DC    F'1'                NCREMENT FACTOR
*++BUILD FIRST LINE  OF THE OUTPUT++++++++++++++++++++++++++++
EMPTY     DC    133CL1' '           BLANK LINE
HEADPADL  DC    50CL1'+'            LEFT PAD TO TITLE
HEAD      DC    CL20'OUTPUT FROM ASSIGN2'     TITLE MESSAGE
HEADPADR  DC    50CL1'+'            RIGHT PAD TO TILE
*++FOLLOWING PRINTS INSIDE LOOP+++++++++++++++++++++++++++++++
CRG       DC    C' '                CARRIAGE FOR PRINT
LCOL1     DC    CL12'          A='  LABEL FOR COL1
COL1      DS    CL12                COL1  VALUE
LCOL2     DC    CL12'          B='  LABEL FOR COL2
COL2      DS    CL12                COL2  VALUE
LCOL3     DC    CL12'          C='  LABEL FOR COL3
COL3      DS    CL12                COL3  VALUE
LSUM      DC    CL12'        SUM='  LABEL FOR SUM
SUMOUT    DS    CL12                SUM OF COLS
*++FOLLOWING PRINTS FINAL RESULTS+++++++++++++++++++++++++++++
FOOT      DC    CL26'  Number of lines: ' TITLE FOR NLINES
NLINES    DS    CL12                VARIABLE FOR NUMBER OF LINES
PDTOTAL   DC    8CL1' '             PADDING WITH WHITE SPACES
FOOT2     DC    CL12'GRAND TOTAL='  TITLE FOR TOTAL
TOTAL     DS    CL12                VARIABLE FOR TOTAL
PAD2      DC    60CL1' '            PADDING WITH WHITE SPACES
MYEND     DC    120CL1'+'           A LONG LINE OF +'es
          END   ASSIGN2
/*
//FT05F001   DD  *
13  59  28
46  89  71
51  83  97
10  07  25
29  67  41
18  36  55
75  48  11
/*
//FT06F001   DD SYSOUT=*
//
